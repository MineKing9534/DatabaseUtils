plugins {
    id 'java'
    id 'idea'

    id'maven-publish'
}

group = 'de.mineking'
version = '1.3.0'

var commit = getCommit()
var release = System.getenv("RELEASE") == "true"

repositories {
    mavenCentral()
    maven { url 'https://maven.mineking.dev/releases' }
}

dependencies {
    implementation 'de.mineking:JavaUtils:1.8.0'

    implementation 'org.jetbrains:annotations:13.0'
    implementation 'org.slf4j:slf4j-api:2.0.9'

    implementation 'org.jdbi:jdbi3-core:3.39.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'ch.qos.logback:logback-classic:1.4.12'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.postgresql:postgresql:42.6.0'
    testImplementation 'org.jdbi:jdbi3-postgres:3.39.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = "https://maven.mineking.dev/" + (release ? "releases" : "snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "de.mineking"
            artifactId = "DatabaseUtils"
            version = release ? this.version : commit

            from components.java
        }
    }
}

test {
    useJUnitPlatform()
}

static String getCommit() {
    try {
        var commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
        return commit.isEmpty() ? 'unknown' : commit
    } catch(IOException e) {
        throw new RuntimeException(e)
    }
}
